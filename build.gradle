buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath group: 'net.sf.saxon', name: 'Saxon-HE', version: saxonVersion
    classpath group: 'biz.aQute.bnd', name: 'biz.aQute.bnd.gradle', version: '5.0.1'
  }
}

plugins {
  id "java"
  id "maven-publish"
  id "signing"
}

import java.util.regex.Pattern
import java.util.regex.Matcher

apply plugin: 'biz.aQute.bnd.builder'

// Version strings
def saxonVersionMatcher = (saxonVersion =~ /^(\d+)\.(\d+)\.(\d+)-(\d+)$/)
if (saxonVersionMatcher.size() != 1) {
  throw new RuntimeException("Saxon version does not match regex?")
}

def sMajor   = saxonVersionMatcher[0][1]
def sMinor   = saxonVersionMatcher[0][2]
def sRelease = saxonVersionMatcher[0][3]
def sPatch   = saxonVersionMatcher[0][4]

project.ext.saxonRelease = sMajor + "." + sMinor + "." + sRelease
project.ext.saxonBranch  = sMajor + sMinor
project.ext.releaseVersion = relVersion
project.ext.distVersion = relVersion + snapshot
relVersion = project.ext.distVersion // - gets turned into . in some contexts

repositories {
  mavenCentral()
  maven { url "https://maven.restlet.org" }
}

configurations {
  copydep {
    extendsFrom implementation
  }
  copydep.exclude module: 'xmlcalabash'
  copydep.exclude module: 'nwalsh-annotations'

  deployerJars
}

configurations.all {
  resolutionStrategy {
    force 'xml-apis:xml-apis:1.4.01'
  }
}

dependencies {
  implementation (
    [group: 'com.nwalsh', name: 'nwalsh-annotations', version: '1.0.1'],
    [group: 'com.xmlcalabash', name: 'xmlcalabash', version: xmlCalabashVersion],
    [group: 'net.sf.xslthl', name: 'xslthl', version: '2.1.3']
  )
}

compileJava {
  options.deprecation = true
  options.compilerArgs += ['-Xlint:deprecation']
  options.annotationProcessorPath = configurations.compileClasspath
}

task copyLib(type: Copy) {
  FileCollection lib = configurations.copydep
  String path = ""
  lib.each {
    File file -> path += " lib/" + file.name
  }
  project.ext.runtimeClasspath = path.trim()

  from lib
  into { "build/dist/lib" }
}

jar {
  archiveBaseName = "xmlcalabash1-xslthl-" + relVersion
  bnd ('Bundle-Vendor': 'Norman Walsh',
       'Bundle-Description': 'XML Calabash 1.x XSLTHL step',
       'Bundle-Name': 'xmlcalabash1-xslthl',
       'Bundle-SymbolicName': 'com.xmlcalabash.extensions.xslthl',
       'Class-Path': project.ext.runtimeClasspath + " lib",
       'Implementation-Title': 'XML Calabash 1.x XSLTHL step',
       'Implementation-Version': project.ext.distVersion
  )
}

task copyNotices(type: Copy) {
  from 'resources/notices'
  into 'build/dist/docs/notices'
}
jar.dependsOn copyNotices

task copyStuff(type: Copy) {
  from 'README.md'
  from 'LICENSE.md'
  into 'build/dist'
}

task copyToDist(dependsOn: [copyLib,copyStuff]) {
  // nop
}
jar.dependsOn copyToDist

task javadocJar(type: Jar, dependsOn: javadoc) {
    from tasks.javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task copyJar(dependsOn: jar, type: Copy) {
  from "build/libs/xmlcalabash1-xslthl-" + distVersion + ".jar"
  into "build/dist"
}

task copyJavaDoc(dependsOn: javadoc, type: Copy) {
  from "build/docs"
  into "build/dist/docs"
}

task testStep(type: JavaExec) {
  classpath = configurations.runtimeClasspath + sourceSets.main.output

  main = 'com.xmlcalabash.drivers.Main'
  maxHeapSize = "1024m"
  args('-D', '-a', 'src/test/resources/test-pipeline.xpl')
}

task makeDist(dependsOn: [ build, copyJar, copyLib ]) {
  doLast {
    println("Created distribution in build/dist")
  }
}

task zipDist(dependsOn: makeDist, type: Zip) {
  from('build/dist')
  into 'xmlcalabash1-xslthl-' + distVersion
  archiveFileName = 'xmlcalabash1-xslthl-' + distVersion + ".zip"
}

task dist(dependsOn: [testStep, zipDist]) {
  // nop
}
signing {
  sign publishing.publications
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'XML Calabash XSLTHL step'
        packaging = 'jar'
        description = 'XML Calabash extension step send xslthl'
        url = 'https://github.com/ndw/xmlcalabash1-xslthl'

        scm {
          url = 'scm:git@github.com:ndw/xmlcalabash1-xslthl.git'
          connection = 'scm:git@github.com:ndw/xmlcalabash1-xslthl.git'
          developerConnection = 'scm:git@github.com:ndw/xmlcalabash1-xslthl.git'
        }

        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'ndw'
            name = 'Norman Walsh'
          }
        }
      }

      groupId = "com.xmlcalabash"
      artifactId = "xmlcalabash1-xslthl"
      version = relVersion
      from components.java
      artifact javadocJar
      artifact sourcesJar
    }
  }

  repositories {
    maven {
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = sonatypeUsername
        password = sonatypePassword
      }
    }
  }
}
