buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath group: 'net.sf.saxon', name: 'Saxon-HE', version: saxonVersion
    classpath group: 'biz.aQute.bnd', name: 'biz.aQute.bnd.gradle', version: '4.2.0'
  }
}

plugins {
  id "java"
  id "maven"
  id "signing"
}

apply plugin: 'biz.aQute.bnd.builder'

repositories {
  mavenCentral()
  maven { url "https://maven.restlet.org" }
}

configurations {
  copydep {
    extendsFrom runtime
  }
  copydep.exclude module: 'xmlcalabash'
  copydep.exclude module: 'nwalsh-annotations'

  deployerJars
}

configurations.all {
  resolutionStrategy {
    force 'xml-apis:xml-apis:1.4.01'
  }
}

dependencies {
  compileOnly (
    [group: 'com.nwalsh', name: 'nwalsh-annotations', version: '1.0.1'],
  )
  annotationProcessor (
    [group: 'com.nwalsh', name: 'nwalsh-annotations', version: '1.0.1'],
  )
  compile (
    [group: 'com.xmlcalabash', name: 'xmlcalabash', version: '1.1.27-98'],
    [group: 'net.sf.xslthl', name: 'xslthl', version: '2.1.3']
  )
}

// Variables
project.ext.saxonRelease = saxonVersion.substring(0,5)
project.ext.saxonBranch  = saxonVersion.substring(0,3).replaceAll("\\.", "")
project.ext.releaseVersion = version
project.ext.distVersion = version + snapshot
version = project.ext.distVersion // - gets turned into . in some contexts

task copyLib(type: Copy) {
  FileCollection lib = configurations.copydep
  String path = ""
  lib.each {
    File file -> path += " lib/" + file.name
  }
  project.ext.runtimeClasspath = path.trim()

  from lib
  into { "build/dist/lib" }
}

jar {
  bnd ('Bundle-Vendor': 'Norman Walsh',
       'Bundle-Description': 'XML Calabash 1.x XSLTHL step',
       'Bundle-Name': 'xmlcalabash1-xslthl',
       'Bundle-SymbolicName': 'com.xmlcalabash.extensinos.xslthl',
       'Class-Path': project.ext.runtimeClasspath + " lib",
       'Implementation-Title': 'XML Calabash 1.x XSLTHL stp',
       'Implementation-Version': project.ext.distVersion
  )
}

task copyNotices(type: Copy) {
  from 'resources/notices'
  into 'build/dist/docs/notices'
}
jar.dependsOn copyNotices

task copyStuff(type: Copy) {
  from 'README.md'
  from 'LICENSE.md'
  into 'build/dist'
}

task copyToDist(dependsOn: [copyLib,copyStuff]) {
  // nop
}
jar.dependsOn copyToDist

task javadocJar(type: Jar, dependsOn: javadoc) {
    from tasks.javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task copyJar(dependsOn: jar, type: Copy) {
  from "build/libs/xmlcalabash1-xslthl-" + distVersion + ".jar"
  into "build/dist"
}

task copyJavaDoc(dependsOn: javadoc, type: Copy) {
  from "build/docs"
  into "build/dist/docs"
}

task testStep(type: JavaExec) {
  classpath = configurations.copydep + configurations.runtime + sourceSets.main.output

  main = 'com.xmlcalabash.drivers.Main'
  maxHeapSize = "1024m"
  args('-D', '-a', 'src/test/resources/test-pipeline.xpl')
}

task makeDist(dependsOn: [ build, copyJar, copyLib ]) {
  doLast {
    println "Created distribution in build/dist"
  }
}

task zipDist(dependsOn: makeDist, type: Zip) {
  from('build/dist')
  into 'xmlcalabash1-xslthl-' + distVersion
  archiveName 'xmlcalabash1-xslthl-' + distVersion + ".zip"
}

task dist(dependsOn: [testStep, zipDist]) {
  // nop
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      configuration = configurations.deployerJars

      beforeDeployment { MavenDeployment deployment ->
        signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      pom.project {
        name 'XML Calabash MarkLogic XSLTHL step'
        packaging 'jar'
        description 'XML Calabash extension step send xslthl'
        url 'https://github.com/ndw/xmlcalabash1-xslthl'

        scm {
          url 'scm:git@github.com:ndw/xmlcalabash1-xslthl.git'
          connection 'scm:git@github.com:ndw/xmlcalabash1-xslthl.git'
          developerConnection 'scm:git@github.com:ndw/xmlcalabash1-xslthl.git'
        }

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id 'ndw'
            name 'Norman Walsh'
          }
        }
      }
    }
  }
}
